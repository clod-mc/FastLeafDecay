plugins {
    id "java-library"
    id 'com.diffplug.spotless' version '6.25.0'
    id 'checkstyle'
}

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT"
}

group = "com.cavetale.fastleafdecay"
version = "./src/build/version.py".execute().text.trim()
java.sourceCompatibility = JavaVersion.VERSION_17
base {
    archivesName = description
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        googleJavaFormat('1.19.2').reflowLongStrings().skipJavadocFormatting()
        formatAnnotations()
    }
}
